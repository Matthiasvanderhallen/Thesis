\begin{lstlisting}[frame=single,numbers=left, language={[x86masm]Assembler}, caption={[LLVM Mask, Unmask and Unmasktype]LLVM IR for the mask, unmask and unmasktype function.},
label=lst:maskcode]
%int = type i64

declare i8* @malloc(%int)
declare void @free(i8*)
declare void @exit(i32)

;This is the type of elements in the masking list.
;It represents the masking list as a linked list.
%masktype = type {%int, %masktype*, %int}

;This is a pointer to the initial linked list element.
@vtable = private global %masktype* null

;The mask() function.
;Because LLVM registers are SSA, it uses a tailrecursive helper function.
define %int @mask(%int %val, %int %type){
	%ret = tail call %int @mask_rec(%int %val, %int %type, %masktype** @vtable, %int 0)
	ret %int %ret
}

define private %int @mask_rec(%int %val, %int %type, %masktype** %cptr, %int %index){
	;Load the current pointer to %masktype* & check if its null. If it is, add a new record to the linked list.
	%current = load %masktype** %cptr
	%check = icmp eq %masktype* %current, null
	switch i1 %check, label %Valcheck [i1 1, label %Add]

	;If the current pointer is null, we're at the end of the linked list
	;Add a record to the linked list by allocating memory for an element, and storing the pointer to it in the current pointer
	;Store value and initialize pointer of the element to null.
	Add:
		%ptr1 = call i8* @malloc(%int 24)
		%ptr = bitcast i8* %ptr1 to %masktype*
		store %masktype* %ptr, %masktype** %cptr
		%locval = getelementptr inbounds %masktype* %ptr, i32 0, i32 0
		store %int %val, %int* %locval
		%locptr = getelementptr inbounds %masktype* %ptr, i32 0, i32 1
		store %masktype* null, %masktype** %locptr
		%loctype = getelementptr inbounds %masktype* %ptr, i32 0, i32 2
		store %int %type, %int* %loctype
		ret %int %index
	
	;If the current pointer is allocated, check the value.
	;if eq -> jump to the return
	;else -> change accumulator, rec jump
	Valcheck:
		%locvalcheck = getelementptr inbounds %masktype* %current, i32 0, i32 0
		%valcheck = load %int* %locvalcheck
		%check2 = icmp eq %int %valcheck, %val
		switch i1 %check2, label %Return [i1 0, label %Loop]
	
	Loop:
		;get pointer to next
		%nextptr = getelementptr inbounds %masktype* %current, i32 0, i32 1
		%newindex = add %int %index, 1
		%tailindex = tail call %int @mask_rec(%int %val, %int %type, %masktype** %nextptr, %int %newindex)
		ret %int %tailindex

	Return:
		ret %int %index
}

define %int @unmask(%int %index){
	%ret = call %int @unmask_rec(%int %index, %masktype** @vtable)
	ret %int %ret
}

define private %int @unmask_rec(%int %cindex, %masktype** %cpointer){
	%current = load %masktype** %cpointer ;Get pointer to current masktype.
	%check = icmp eq %masktype* %current, null
	br i1 %check, label %Error, label %ZeroTest

	Error:
		call void @exit(i32 -1)
  		unreachable

	ZeroTest:
		%check2 = icmp eq %int %cindex, 0
		br i1 %check2, label %RetVal, label %Loop

	RetVal:
		%locval = getelementptr inbounds %masktype* %current, i32 0, i32 0 ; Get pointer to val pointer
		%val = load %int* %locval
		ret %int %val

	Loop:
		%nextptr = getelementptr inbounds %masktype* %current, i32 0, i32 1 ; Get pointer to next ll-element pointer.
		%newindex = sub %int %cindex, 1
		%ret = call %int @unmask_rec(%int %newindex, %masktype** %nextptr)
		ret %int %ret
}

define %int @unmasktype(%int %index){
	%ret = call %int @unmasktype_rec(%int %index, %masktype** @vtable)
	ret %int %ret
}

define private %int @unmasktype_rec(%int %cindex, %masktype** %cpointer){
	%current = load %masktype** %cpointer ;Get pointer to current masktype.
	%check = icmp eq %masktype* %current, null
	br i1 %check, label %Error, label %ZeroTest

	Error:
		call void @exit(i32 -1)
  		unreachable

	ZeroTest:
		%check2 = icmp eq %int %cindex, 0
		br i1 %check2, label %RetVal, label %Loop

	RetVal:
		%loctype = getelementptr inbounds %masktype* %current, i32 0, i32 2  ; Get pointer to type pointer
		%type = load %int* %loctype
		ret %int %type

	Loop:
		%nextptr = getelementptr inbounds %masktype* %current, i32 0, i32 1  ; Get pointer to next ll-element pointer.
		%newindex = sub %int %cindex, 1
		%ret = call %int @unmasktype_rec(%int %newindex, %masktype** %nextptr)
		ret %int %ret
}
\end{lstlisting}